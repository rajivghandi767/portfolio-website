@Library('homelab-library') _

pipeline {
    agent any

    environment {
        DOCKER_BUILDKIT     = "1"
        APP_NAME            = "Portfolio Website"
        PROJECT_NAME        = "portfolio" 
        REGISTRY            = "ghcr.io"
        REGISTRY_CRED_ID    = "github-packages-pat"
        VAULT_CRED_ID       = "vault-${PROJECT_NAME}-approle"
        VAULT_URL           = "http://vault:8200"
        VAULT_SECRET_PATH   = "secret/${PROJECT_NAME}-prod"

        IMAGE_BACKEND       = "rajivghandi767/${PROJECT_NAME}-backend"
        IMAGE_FRONTEND      = "rajivghandi767/${PROJECT_NAME}-frontend"
        IMAGE_NGINX         = "rajivghandi767/${PROJECT_NAME}-nginx"
        
        DISCORD_SUCCESS_COLOR = "3066993"
        DISCORD_FAIL_COLOR    = "15158332"
    }

    stages {
        stage('🔍 Vault Status & Unseal') {
            steps {
                script {
                    unsealVault()
                }
            }
        }

        stage('Checkout') {
            steps { checkout scm }
        }

        stage('Test Suite') {
            parallel {
                stage('Test Backend (Python)') {
                    steps {
                        sh '''
                            docker run --rm \
                                -v "$(pwd)/backend:/app" \
                                -w /app \
                                python:3.14-slim \
                                sh -c "pip install --no-cache-dir -r dev-requirements.txt && python manage.py test"
                        '''
                    }
                }
                stage('Test Frontend (React)') {
                    steps {
                        sh '''
                            docker run --rm \
                                -v "$(pwd)/frontend:/app" \
                                -w /app \
                                node:22-alpine \
                                sh -c "npm ci && npm run test"
                        '''
                    }
                }
            }
        }

        stage('Build & Push Images') {
            steps {
                withVault(configuration: [vaultUrl: "${env.VAULT_URL}", vaultCredentialId: "${env.VAULT_CRED_ID}", engineVersion: 2], 
                vaultSecrets: [[path: "${env.VAULT_SECRET_PATH}", secretValues: [
                    [envVar: 'VITE_API_URL', vaultKey: 'VITE_API_URL']
                ]]]) {
                    script {
                        def gitCommit = sh(returnStdout: true, script: 'git rev-parse --short HEAD').trim()

                        docker.withRegistry("https://${env.REGISTRY}", env.REGISTRY_CRED_ID) {
                            parallel(
                                "Backend": {
                                    def img = docker.build("${env.REGISTRY}/${env.IMAGE_BACKEND}:${env.BUILD_NUMBER}", "--label git-commit=${gitCommit} -f backend/Dockerfile.prod ./backend")
                                    img.push()
                                    img.push("latest")
                                },
                                "Frontend": {
                                    def img = docker.build("${env.REGISTRY}/${env.IMAGE_FRONTEND}:${env.BUILD_NUMBER}", "--label git-commit=${gitCommit} -f frontend/Dockerfile.prod --build-arg VITE_API_URL=${env.VITE_API_URL} ./frontend")
                                    img.push()
                                    img.push("latest")
                                },
                                "Nginx": {
                                    def img = docker.build("${env.REGISTRY}/${env.IMAGE_NGINX}:${env.BUILD_NUMBER}", "--label git-commit=${gitCommit} ./nginx")
                                    img.push()
                                    img.push("latest")
                                }
                            )
                        }
                    }
                }
            }
        }
    }

    post {
        success {
            script {
                def msg = "Build **#${env.BUILD_NUMBER}** completed successfully.\n[View Jenkins Logs](${env.BUILD_URL})"
                notifyDiscord("✅ ${env.APP_NAME} Build Success", msg, env.DISCORD_SUCCESS_COLOR.toInteger())
            }
        }
        failure {
            script {
                def msg = "Check Jenkins logs for build **#${env.BUILD_NUMBER}**.\n[View Jenkins Logs](${env.BUILD_URL})"
                notifyDiscord("🚨 ${env.APP_NAME} Build Failed", msg, env.DISCORD_FAIL_COLOR.toInteger())
            }
        }
    }
}